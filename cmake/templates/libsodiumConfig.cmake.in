if(HUNTER_ENABLED)
    include(hunter_internal_error)
    include(hunter_status_debug)
else()
    function(hunter_internal_error)
        message(FATAL_ERROR ${ARGV})
    endfunction()

    function(hunter_status_debug)
        message(${ARGV})
    endfunction()
endif()

get_filename_component(_install_dir "${CMAKE_CURRENT_LIST_DIR}/../../../" ABSOLUTE)


# Get lib file
if(MSVC)
    if(@IS_DEBUG@)
        set(_lib_file ${_install_dir}/lib/libsodiumd.lib)
    else()
        set(_lib_file ${_install_dir}/lib/libsodium.lib)
    endif()
else()
    if(@BUILD_SHARED_LIBS@)
        set(_lib_file ${_install_dir}/lib/libsodium.so)
    else()
        set(_lib_file ${_install_dir}/lib/libsodium.a)
    endif()
endif()


if(NOT TARGET "libsodium::libsodium")
    add_library("libsodium::libsodium" INTERFACE IMPORTED)
    set_target_properties("libsodium::libsodium"
        PROPERTIES
        INTERFACE_LINK_LIBRARIES ${_lib_file})
    set_target_properties("libsodium::libsodium"
        PROPERTIES
        INTERFACE_INCLUDE_DIRECTORIES
        "${_install_dir}/include")
    if(NOT @BUILD_SHARED_LIBS@)
        set_target_properties("libsodium::libsodium"
            PROPERTIES
            INTERFACE_COMPILE_DEFINITIONS
            "SODIUM_STATIC")
    endif()
endif()
