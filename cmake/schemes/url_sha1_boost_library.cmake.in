# Copyright (c) 2013, Ruslan Baratov
# All rights reserved.

cmake_minimum_required(VERSION 2.8.10) # SHA1

include(ExternalProject) # ExternalProject_Add

# Scheme for download and install boost library

list(APPEND CMAKE_MODULE_PATH "@HUNTER_ROOT@/cmake/modules")

include(hunter_fatal_error)
include(hunter_test_string_not_empty)

# Check preconditions
hunter_test_string_not_empty("@HUNTER_BASE@")
hunter_test_string_not_empty("@HUNTER_PACKAGE_NAME@")
hunter_test_string_not_empty("@HUNTER_PACKAGE_DOWNLOAD_DIR@")
hunter_test_string_not_empty("@HUNTER_PACKAGE_SOURCE_DIR@")
hunter_test_string_not_empty("@HUNTER_PACKAGE_INSTALL_DIR@")
hunter_test_string_not_empty("@HUNTER_PACKAGE_BOOST_LIBRARY@")
hunter_test_string_not_empty("@CMAKE_CXX_COMPILER@")

set_directory_properties(
    PROPERTIES
    EP_BASE
    @HUNTER_BASE@
)

set(
    boost_libs
    atomic
    chrono
    context
    coroutine
    date_time
    exception
    filesystem
    graph
    graph_parallel
    iostreams
    locale
    log
    math
    mpi
    program_options
    python
    random
    regex
    serialization
    signals
    system
    test
    thread
    timer
    wave
)

set(libfound NO)
foreach(x ${boost_libs})
  string(COMPARE EQUAL "${x}" "@HUNTER_PACKAGE_BOOST_LIBRARY@" result)
  if(result)
    set(libfound YES)
  endif()
endforeach()

if(NOT libfound)
  hunter_fatal_error("No such library")
endif()

string(COMPARE EQUAL "${CMAKE_CXX_COMPILER_ID}" "Clang" compiler_is_clang)

if(APPLE)
  set(toolset_name darwin)
elseif(CMAKE_COMPILER_IS_GNUCXX)
  set(toolset_name gcc)
elseif(compiler_is_clang)
  set(toolset_name clang)
else()
  hunter_fatal_error("TODO: set toolset for boost")
endif()

set(boost_user_jam @HUNTER_BASE@/boost.user.jam)
file(
     WRITE
     ${boost_user_jam}
     "using ${toolset_name} :\n"
     "    : ${CMAKE_CXX_COMPILER} ${CMAKE_CXX_FLAGS}\n"
     ";\n"
)

set(
    build_opts
    -a
    link=static,shared
    threading=multi
    variant=release,debug
    --layout=tagged
    toolset=${toolset_name}
    --user-config=${boost_user_jam}
)

if(@HUNTER_STATUS_DEBUG@)
  set(verbose_output "-d+2")
endif()

ExternalProject_Add(
    @HUNTER_PACKAGE_NAME@_@HUNTER_PACKAGE_BOOST_LIBRARY@
    URL
    DOWNLOAD_COMMAND
    ${CMAKE_COMMAND} -E echo "Skip download"
    DOWNLOAD_DIR
    @HUNTER_PACKAGE_DOWNLOAD_DIR@
    SOURCE_DIR
    @HUNTER_PACKAGE_SOURCE_DIR@
    INSTALL_DIR
    @HUNTER_PACKAGE_INSTALL_DIR@
        # not used, just avoid creating Install/<name> empty directory
    CONFIGURE_COMMAND
    ./bootstrap.sh
    --with-libraries=@HUNTER_PACKAGE_BOOST_LIBRARY@
    --prefix=@HUNTER_PACKAGE_INSTALL_DIR@
    --with-toolset=${toolset_name}
    BUILD_COMMAND
    ./b2
    ${verbose_output}
    ${build_opts}
    BUILD_IN_SOURCE
    1
    INSTALL_COMMAND
    ./b2
    -d0
    ${build_opts}
    install
)
