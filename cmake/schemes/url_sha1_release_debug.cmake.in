# Copyright (c) 2013, Ruslan Baratov
# All rights reserved.

cmake_minimum_required(VERSION 3.0)
project(Hunter LANGUAGES NONE)

include(ExternalProject) # ExternalProject_Add

# Scheme for separated Release/Debug builds (like make)

list(APPEND CMAKE_MODULE_PATH "@HUNTER_SELF@/cmake/modules")

include(hunter_gate_settings)
include(hunter_internal_error)
include(hunter_status_debug)
include(hunter_test_string_not_empty)

hunter_status_debug("Scheme: url_sha1_release_debug")

if("@XCODE_VERSION@")
  hunter_internal_error("Not for Xcode")
endif()

if("@MSVC_IDE@")
  hunter_internal_error("Not for MSVC IDE")
endif()

# Check preconditions
hunter_test_string_not_empty("@HUNTER_SELF@")
hunter_test_string_not_empty("@HUNTER_EP_NAME@")
hunter_test_string_not_empty("@HUNTER_PACKAGE_URL@")
hunter_test_string_not_empty("@HUNTER_PACKAGE_SHA1@")
hunter_test_string_not_empty("@HUNTER_PACKAGE_DOWNLOAD_DIR@")
hunter_test_string_not_empty("@HUNTER_PACKAGE_SOURCE_DIR@")
hunter_test_string_not_empty("@HUNTER_INSTALL_PREFIX@")
hunter_test_string_not_empty("@CMAKE_DEBUG_POSTFIX@")
hunter_test_string_not_empty("${CMAKE_TOOLCHAIN_FILE}")

if("@HUNTER_STATUS_DEBUG@")
  set(verbose_makefile "-DCMAKE_VERBOSE_MAKEFILE=ON")
endif()

hunter_gate_settings(gate_settings)

ExternalProject_Add(
    "@HUNTER_EP_NAME@-release"
    URL
    @HUNTER_PACKAGE_URL@
    URL_HASH
    SHA1=@HUNTER_PACKAGE_SHA1@
    DOWNLOAD_DIR
    "@HUNTER_PACKAGE_DOWNLOAD_DIR@"
    SOURCE_DIR
    "@HUNTER_PACKAGE_SOURCE_DIR@"
    INSTALL_DIR
    "@HUNTER_INSTALL_PREFIX@"
        # not used, just avoid creating Install/<name>_Release empty directory
    CMAKE_ARGS
    "-DCMAKE_BUILD_TYPE=Release"
    "-DCMAKE_INSTALL_PREFIX=@HUNTER_INSTALL_PREFIX@"
    ${verbose_makefile}
    ${gate_settings}
    "-DCMAKE_TOOLCHAIN_FILE=${CMAKE_TOOLCHAIN_FILE}"
)

# download and source step will use previous target source dir (skipped)
ExternalProject_Add(
    "@HUNTER_EP_NAME@-debug"
    DOWNLOAD_COMMAND
    ""
    DOWNLOAD_DIR
    "@HUNTER_PACKAGE_DOWNLOAD_DIR@"
    SOURCE_DIR
    "@HUNTER_PACKAGE_SOURCE_DIR@"
    INSTALL_DIR
    "@HUNTER_INSTALL_PREFIX@"
    CMAKE_ARGS
    "-DCMAKE_BUILD_TYPE=Debug"
    "-DCMAKE_INSTALL_PREFIX=@HUNTER_INSTALL_PREFIX@"
    "-DCMAKE_DEBUG_POSTFIX=@CMAKE_DEBUG_POSTFIX@"
    ${verbose_makefile}
    ${gate_settings}
    "-DCMAKE_TOOLCHAIN_FILE=${CMAKE_TOOLCHAIN_FILE}"
)

# debug version use release download result
add_dependencies(
    "@HUNTER_EP_NAME@-debug"
    "@HUNTER_EP_NAME@-release"
)
