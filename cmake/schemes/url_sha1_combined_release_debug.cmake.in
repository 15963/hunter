# Copyright (c) 2013, Ruslan Baratov
# All rights reserved.

cmake_minimum_required(VERSION 2.8.10) # SHA1

include(ExternalProject) # ExternalProject_Add

# Scheme for Xcode and MSVC IDE:
#     install both Debug and Release from one project

list(APPEND CMAKE_MODULE_PATH "@HUNTER_ROOT@/cmake/modules")

include(hunter_fatal_error)

if(NOT XCODE_VERSION AND NOT MSVC)
  hunter_fatal_error("Xcode or MSVC only")
endif()

macro(check_not_empty var)
  if("--${var}__" STREQUAL "--__")
    hunter_fatal_error(FATAL_ERROR "Variable is empty")
  endif()
endmacro()

# Check preconditions
check_not_empty(@HUNTER_BASE@)
check_not_empty(@HUNTER_PACKAGE_NAME@)
check_not_empty(@HUNTER_PACKAGE_URL@)
check_not_empty(@HUNTER_PACKAGE_SHA1@)
check_not_empty(@HUNTER_PACKAGE_DOWNLOAD_DIR@)
check_not_empty(@HUNTER_PACKAGE_SOURCE_DIR@)
check_not_empty(@HUNTER_PACKAGE_INSTALL_DIR@)
check_not_empty(@CMAKE_DEBUG_POSTFIX@)
check_not_empty(${CMAKE_TOOLCHAIN_FILE})

set_directory_properties(
    PROPERTIES
    EP_BASE
    @HUNTER_BASE@
)

ExternalProject_Add(
    @HUNTER_PACKAGE_NAME@
    URL
    @HUNTER_PACKAGE_URL@
    URL_HASH
    SHA1=@HUNTER_PACKAGE_SHA1@
    DOWNLOAD_DIR
    @HUNTER_PACKAGE_DOWNLOAD_DIR@
    SOURCE_DIR
    @HUNTER_PACKAGE_SOURCE_DIR@
    INSTALL_DIR
    @HUNTER_PACKAGE_INSTALL_DIR@
        # not used, just avoid creating Install/<name> empty directory
    CMAKE_ARGS
    "-DCMAKE_DEBUG_POSTFIX=@CMAKE_DEBUG_POSTFIX@"
    "-DCMAKE_INSTALL_PREFIX=@HUNTER_PACKAGE_INSTALL_DIR@"
    "-DCMAKE_TOOLCHAIN_FILE=${CMAKE_TOOLCHAIN_FILE}"
    INSTALL_COMMAND
    ${CMAKE_COMMAND} --build . --target install --config Release
    COMMAND
    ${CMAKE_COMMAND} --build . --target install --config Debug
)
