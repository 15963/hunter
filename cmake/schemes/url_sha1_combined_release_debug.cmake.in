# Copyright (c) 2013, 2015 Ruslan Baratov
# All rights reserved.

cmake_minimum_required(VERSION 3.0)
project(Hunter LANGUAGES NONE)

include(ExternalProject) # ExternalProject_Add

# Scheme for Xcode and MSVC IDE:
#     install both Debug and Release from one project

list(APPEND CMAKE_MODULE_PATH "@HUNTER_SELF@/cmake/modules")

include(hunter_gate_settings)
include(hunter_internal_error)
include(hunter_status_debug)
include(hunter_test_string_not_empty)

hunter_status_debug("Scheme: url_sha1_combined_release_debug")

string(COMPARE NOTEQUAL "@XCODE_VERSION@" "" is_xcode)
if(NOT is_xcode AND NOT "@MSVC_IDE@")
  hunter_internal_error("Xcode or MSVC IDE only")
endif()

# Check preconditions
hunter_test_string_not_empty("@HUNTER_SELF@")
hunter_test_string_not_empty("@HUNTER_EP_NAME@")
hunter_test_string_not_empty("@HUNTER_PACKAGE_URL@")
hunter_test_string_not_empty("@HUNTER_PACKAGE_SHA1@")
hunter_test_string_not_empty("@HUNTER_PACKAGE_DOWNLOAD_DIR@")
hunter_test_string_not_empty("@HUNTER_PACKAGE_SOURCE_DIR@")
hunter_test_string_not_empty("@HUNTER_INSTALL_PREFIX@")
hunter_test_string_not_empty("@CMAKE_DEBUG_POSTFIX@")
hunter_test_string_not_empty("${CMAKE_TOOLCHAIN_FILE}")

string(COMPARE NOTEQUAL "@HUNTER_JOBS_OPTION@" "" have_jobs)
if(is_xcode AND have_jobs)
  set(jobs_option "-jobs" "@HUNTER_JOBS_OPTION@")
else()
  set(jobs_option "")
endif()

hunter_gate_settings(gate_settings)

ExternalProject_Add(
    "@HUNTER_EP_NAME@"
    URL
    @HUNTER_PACKAGE_URL@
    URL_HASH
    SHA1=@HUNTER_PACKAGE_SHA1@
    DOWNLOAD_DIR
    "@HUNTER_PACKAGE_DOWNLOAD_DIR@"
    SOURCE_DIR
    "@HUNTER_PACKAGE_SOURCE_DIR@"
    INSTALL_DIR
    "@HUNTER_INSTALL_PREFIX@"
        # not used, just avoid creating Install/<name> empty directory
    CMAKE_ARGS
    "-DCMAKE_DEBUG_POSTFIX=@CMAKE_DEBUG_POSTFIX@"
    "-DCMAKE_INSTALL_PREFIX=@HUNTER_INSTALL_PREFIX@"
    "-DCMAKE_TOOLCHAIN_FILE=${CMAKE_TOOLCHAIN_FILE}"
    ${gate_settings}
    INSTALL_COMMAND
    "@CMAKE_COMMAND@" --build . --target install --config Release -- ${jobs_option}
    COMMAND
    "@CMAKE_COMMAND@" --build . --target install --config Debug -- ${jobs_option}
)
